plugins {
  id 'java'
  alias libs.plugins.spring.boot
  alias libs.plugins.spring.dependencies
  alias libs.plugins.jib
}

configurations {
  compileOnly.extendsFrom annotationProcessor
}

dependencies {
  implementation project(':packages:contracts')

  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'com.auth0:java-jwt:4.3.0'
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
  implementation 'com.amazonaws:aws-java-sdk-s3:1.12.437'
  
  compileOnly 'org.projectlombok:lombok'

  developmentOnly project(':packages:migrations')
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  
  runtimeOnly 'org.postgresql:postgresql'

  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.projectlombok:lombok'
  
  testAnnotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation project(':packages:migrations')
}

tasks.named('test') {
  useJUnitPlatform()
}

jib {
  to {
    image = 'ghcr.io/thedatasnok/webshop-backend'
    tags = ['latest', version]
  }

  container {
    ports = ['8080']
    labels = [
      'org.opencontainers.image.source': 'https://github.com/thedatasnok/webshop',
      'org.opencontainers.image.description': 'Webshop backend container',
      'org.opencontainers.licenses': 'MIT'
    ]
  }
}